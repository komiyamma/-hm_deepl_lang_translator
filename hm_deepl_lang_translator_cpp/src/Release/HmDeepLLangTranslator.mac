
$auth_key = "*******-****-****-****-************:**";
$auth_key = getenv("DEEPL_SCRIPT_TRANSLATION"); // 上行のようなマクロ内への直書きは避けることをオススメします。ご自身で環境変数「DEEPL_SCRIPT_TRANSLATION」にDeepLのAPIのAUTH_KEYを設定してください。

if (strstr(filename, "hmdeepllangtranslator") >= 0) {
    // このマクロ自身に対して実行するのは、
    // マクロのソース中に文字列を挿入してしまうため
    // 意図せずマクロを破壊してしまう可能性が高い。
    // 
    // ほぼ確実に意図しない操作とみなして、止める
    message("「HmDeepLLangTranslator自身」を翻訳対象にはできません。");
    endmacro;
}

$source_lang = getarg(0); // 今のところ利用していない
$target_lang = getarg(1);

#DLL_CURL = loaddll( currentmacrodirectory + @"\libcurl.dll"); // 遅延読み込みになってないので、依存ライブラリ先に呼んでおく。
#DLL = loaddll( currentmacrodirectory + @"\HmDeepLLangTranslator.dll");

// 途中でマクロの文法や型の致命的エラーが発生した場合でもdllを解放する。
if (version >= 898) { 
    eval "keepdll #DLL, 0";
    eval "keepdll #DLL_CURL, 0";
}

$AnswerText = dllfuncstrw(#DLL, "try_translate" , $auth_key, $source_lang, $target_lang);

freedll(#DLL);
freedll(#DLL_CURL);


if (selecting) {
    escape;
    moveto selendx, selendy; // 選択してたら解除しつつも選択範囲の最後尾に
} else {
    gofileend; // 最後尾に
}

insert "\n" + $AnswerText;

